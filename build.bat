@echo off
setlocal EnableExtensions EnableDelayedExpansion
title Build CS2ChatTranslationBot

:: === Settings ===
set APP_NAME=CS2ChatTranslationBot
set APP_EXE_NAME=CS2ChatTranslationBot_app
set LAUNCHER_SCRIPT=launcher.py
set ENTRY_POINT=app\main.py
set LANG_SRC_DIR=app\lang
set DIST_DIR=dist\%APP_NAME%
set CURRENT_DIR=%DIST_DIR%\current
set NUITKA_BUILD_DIR=.nuitka_build

echo.
echo Building %APP_NAME% with Nuitka...
echo.

:: --------------------------------------------------------------------------------
:: Create build number from current date/time
for /f %%A in ('powershell -NoProfile -Command "Get-Date -Format \"yyddMMHHmm\""') do set BUILDNUMBER=%%A
set CURRENT_VERSION=0.6.%BUILDNUMBER%

:: Windows file version must be exactly 4 parts each <= 65535: 0.6.YYMM.DDmm
for /f %%A in ('powershell -NoProfile -Command "Get-Date -Format \"yyMM.ddmm\""') do set WIN_VERSION=0.6.%%A

echo CURRENT_VERSION = %CURRENT_VERSION%
echo WIN_VERSION     = %WIN_VERSION%
:: --------------------------------------------------------------------------------

:: Write version module (imported by app at runtime)
echo CURRENT_VERSION = "%CURRENT_VERSION%" > app\_build_version.py

:: Clean old builds
rmdir /s /q build 2>nul
rmdir /s /q dist 2>nul
rmdir /s /q %NUITKA_BUILD_DIR% 2>nul

:: --------------------------------------------------------------------------------
:: Build main app as standalone into current\ subfolder
echo.
echo Building main app...
python -m nuitka ^
 --standalone ^
 --windows-console-mode=disable ^
 --output-filename=%APP_EXE_NAME%.exe ^
 --output-dir=%NUITKA_BUILD_DIR% ^
 --enable-plugin=tk-inter ^
 --windows-company-name="Crankerer" ^
 --windows-product-name="CS2 Chat Translation Bot" ^
 --windows-file-version=%WIN_VERSION% ^
 --windows-product-version=%WIN_VERSION% ^
 %ENTRY_POINT%

if %errorlevel% neq 0 (
    echo Build failed!
    pause
    exit /b %errorlevel%
)

:: Move Nuitka output (main.dist) to dist\APP_NAME\current
mkdir "%DIST_DIR%" 2>nul
move "%NUITKA_BUILD_DIR%\main.dist" "%CURRENT_DIR%"

:: --------------------------------------------------------------------------------
:: Build launcher as standalone
echo.
echo Building launcher...
python -m nuitka ^
 --standalone ^
 --windows-console-mode=disable ^
 --output-filename=%APP_NAME%.exe ^
 --output-dir=%NUITKA_BUILD_DIR% ^
 --windows-company-name="Crankerer" ^
 --windows-product-name="CS2 Chat Translation Bot" ^
 --windows-file-version=%WIN_VERSION% ^
 --windows-product-version=%WIN_VERSION% ^
 %LAUNCHER_SCRIPT%

if %errorlevel% neq 0 (
    echo Launcher build failed!
    pause
    exit /b %errorlevel%
)

:: Copy launcher standalone contents into root of dist folder
xcopy /Y /E /I "%NUITKA_BUILD_DIR%\launcher.dist\*" "%DIST_DIR%\" >nul

:: --------------------------------------------------------------------------------
:: Copy language files into current\lang
echo.
echo Copying language files to %CURRENT_DIR%\lang ...
if not exist "%CURRENT_DIR%\lang" mkdir "%CURRENT_DIR%\lang"
copy "%LANG_SRC_DIR%\lang_*.json" "%CURRENT_DIR%\lang" >nul

:: Write VERSION.txt into root of dist folder
echo %CURRENT_VERSION% > "%DIST_DIR%\VERSION.txt"

:: Cleanup Nuitka temp build folder
rmdir /s /q %NUITKA_BUILD_DIR% 2>nul

echo.
echo Build completed successfully!
echo Output: %DIST_DIR%
echo Version: %CURRENT_VERSION%
echo.
echo Root:
dir /b "%DIST_DIR%"
echo current\:
dir /b "%CURRENT_DIR%"
echo.

endlocal
